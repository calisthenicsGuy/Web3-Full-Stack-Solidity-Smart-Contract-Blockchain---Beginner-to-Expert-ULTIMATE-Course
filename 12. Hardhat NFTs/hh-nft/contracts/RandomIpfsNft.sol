// SPDX-License-Identifier:MIT

pragma solidity ^0.8.7;

import "@chainlink/contracts/src/v0.8/interfaces/VRFCoordinatorV2Interface.sol";
import "@chainlink/contracts/src/v0.8/VRFConsumerBaseV2.sol";
import "@openzeppelin/contracts/token/ERC721/extensions/ERC721URIStorage.sol";
import "@openzeppelin/contracts/access/Ownable.sol";

error RandomIpfsNft__ArrayOutOfBounds();
error RandomIpfsNft__NeedMoreETHSend();
error RandomIpfsNft__TransferFailed();

contract RandomIpfsNft is VRFConsumerBaseV2, ERC721URIStorage, Ownable {
    enum Breed {
        PUG,
        SHIBA_INU,
        ST_BERNARD
    }

    VRFCoordinatorV2Interface private immutable i_vrfCoordinator;
    uint64 private immutable i_subscriptionId;
    bytes32 private immutable i_gasLane;
    uint32 private immutable i_callbackGasLimit;
    uint16 private constant REQUEST_CONFIRMATIONS = 3;
    uint32 private constant NUM_WORDS = 1;

    mapping(uint256 => address) public s_requestIdToSender;
    uint256 private s_tokenCounter;
    string[] private s_tokenUris;
    uint256 private immutable i_mintFee;

    event NftRequested(uint256 indexed requestId, address requester);
    event NftMinted(Breed dogBreed, address minter);

    constructor(
        address vrfCoordinatorV2,
        uint64 subscriptionId,
        bytes32 gasLane,
        uint32 callbackGasLimit,
        string[3] memory tokenUris,
        uint256 mintFee
    ) VRFConsumerBaseV2(vrfCoordinatorV2) ERC721("Random Ipfs Nft", "RIN") {
        i_vrfCoordinator = VRFCoordinatorV2Interface(vrfCoordinatorV2);
        i_subscriptionId = subscriptionId;
        i_gasLane = gasLane;
        i_callbackGasLimit = callbackGasLimit;
        s_tokenUris = tokenUris;
        i_mintFee = mintFee;

        s_tokenCounter = 0;
    }

    function requestNft() public payable returns (uint256 requestId) {
        if (msg.value < i_mintFee) {
            revert RandomIpfsNft__NeedMoreETHSend();
        }

        requestId = i_vrfCoordinator.requestRandomWords(
            i_gasLane,
            i_subscriptionId,
            REQUEST_CONFIRMATIONS,
            i_callbackGasLimit,
            NUM_WORDS
        );

        s_requestIdToSender[requestId] = msg.sender;
        emit NftRequested(requestId, msg.sender);
    }

    function fulfillRandomWords(uint256 requestId, uint256[] memory randomWords)
        internal
        override
    {
        address dogOwner = s_requestIdToSender[requestId];
        uint256 newTokenId = s_tokenCounter;

        uint256 moddedRng = randomWords[0] % 100;
        Breed dogBreed = getBreedFromModdedRng(moddedRng);

        s_tokenCounter++;

        _safeMint(dogOwner, newTokenId);
        _setTokenURI(newTokenId, s_tokenUris[uint256(dogBreed)]);

        emit NftMinted(dogBreed, dogOwner);
    }

    function getBreedFromModdedRng(uint256 moddedRng)
        public
        pure
        returns (Breed)
    {
        uint256 cumulativeSum = 0;
        uint8[3] memory chancheArray = getChanceArray();

        for (uint i = 0; i < chancheArray.length; i++) {
            if (moddedRng >= cumulativeSum && moddedRng <= chancheArray[i]) {
                return Breed(i);
            }

            cumulativeSum += chancheArray[i];
        }

        revert RandomIpfsNft__ArrayOutOfBounds();
    }

    function withdraw() public onlyOwner {
        uint256 amount = address(this).balance;

        (bool success, ) = payable(msg.sender).call{value: amount}("");

        if (!success) {
            revert RandomIpfsNft__TransferFailed();
        }
    }

    function getDogTokenUris(uint256 index) public view returns(string memory) {
        return s_tokenUris[index];
    }

    function getChanceArray() public pure returns (uint8[3] memory) {
        return [10, 30, 100];
    }

    function getMintFee() public view returns(uint256) {
        return i_mintFee;
    }

    function getTokenUris(uint256 index) public view returns(string memory) {
        return s_tokenUris[index];
    }

    function getTokenCounter() public view returns(uint256) {
        return s_tokenCounter;
    }

    function getSuscriptionId() public view returns(uint256) {
        return i_subscriptionId;
    }

    function getGasLane() public view returns(bytes32) {
        return i_gasLane;
    }

    function getCallbackGasLimit() public view returns(uint32) {
        return i_callbackGasLimit;
    }

    function getRequestConfirmations() public pure returns(uint16) {
        return REQUEST_CONFIRMATIONS;
    }

    function getNumWords() public pure returns(uint32) {
        return NUM_WORDS;
    }
}
